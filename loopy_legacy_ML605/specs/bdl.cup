package de.hopp.generator.frontend;

import java_cup.runtime.*;
import katja.common.NE;
import de.hopp.generator.ErrorCollection;
import de.hopp.generator.exceptions.ParserError;
import de.hopp.generator.model.*;

/* Preliminaries to set up and use the scanner.  */
//init with {: scanner.init(); :};
//scan with {: return getScanner().next_token(); :};

action code {:
  void error(String message, int line, int column) {
    parser.errors.addError(new ParserError("Syntax Error: " + message, parser.filename, line));
  }

  Position pos(int line) {
     return BDL.Position(parser.filename, line);
  }
:}

parser code {:
//  protected BDLFile board = BDL.BDLFile(BDL.Checksum(""), BDL.Imports(), BDL.Logs(BDL.NONE(), BDL.NONE()), BDL.Options(), BDL.DEFAULT(), BDL.Medium("", ""), BDL.Cores(), BDL.GPIOs());
//
//  public BDLFile getBDLFile() {
//    return board;
//  }

  String filename;

  ErrorCollection errors;

  public void setFilename(String filename) {
    this.filename = filename;
  }

  public void setErrorCollection(ErrorCollection errors) {
    this.errors = errors;
  }

  public void report_error(String message, Object info) {

    int linenumber = -1;
    if(info instanceof Symbol) {
      Symbol symbol = (Symbol)info;
      linenumber = symbol.left;
    }
    errors.addError(new ParserError(message, filename, linenumber));
  }

  public void syntax_error(Symbol cur_token) {
//    report_error("Syntax Error in BDL file", cur_token);
  }

  public void unrecovered_syntax_error(Symbol cur_token) {
    errors.addError(new ParserError("Fatal Error: Couldn't repair and continue parse", filename, cur_token.left));
    done_parsing();
  }
:}

/* terminals */
terminal BEGIN, END, CBEGIN, COMMA;
terminal String CEND;
terminal IMPORT;
terminal SWQUEUE, HWQUEUE, LOG, WIDTH, POLL;
terminal MEDIUM, MAC, IP, MASK, GATE, TIMEOUT, DHCP;
terminal CORE, SOURCE, PORT;
terminal CLK, RST;
terminal IN, OUT, DUAL;
terminal GPIO, INSTANCE, BIND, CPU;
terminal SCHEDULER;
terminal String STRING_LITERAL;
terminal String ID, VER;
terminal Integer DEC;

/* non terminals */
non terminal BDLFile file;
non terminal Import imp;
non terminal Medium medium;
non terminal Option option;
non terminal MOption medium_option;
non terminal Core core;
non terminal String version;
non terminal Ports port;
non terminal Instance instance;
non terminal GPIO gpio;
non terminal Bindings bind;
non terminal Scheduler scheduler;

non terminal Logs log;
non terminal LogSeverity severity;
non terminal Boolean device;

non terminal Decl decl;
non terminal Decls decl_list;

non terminal Imports source_list, source;
non terminal Strings string_list;

non terminal Ports port_list;
non terminal Bindings bind_block, bind_list_part;
non terminal Code code_block;
non terminal Options option_list, option_list_part;
non terminal MOptions medium_option_list, medium_option_list_part;

non terminal Direction direct;
non terminal Strings id_list, id_list_part;
non terminal String id;


/* grammar */
start with file;

file ::=
     decl_list:l
     {:
         Checksum checksum   = BDL.Checksum("");
         Imports imports     = BDL.Imports();
         Logs logs           = BDL.Logs(BDL.NONE(), BDL.NONE());
         Options opts        = BDL.Options();
         Cores cores         = BDL.Cores();
         GPIOs gpios         = BDL.GPIOs();
         Instances insts     = BDL.Instances();
         Medium medium       = BDL.NONE();
         Scheduler scheduler = BDL.Scheduler(pos(-1), BDL.DEFAULT());
         for(Decl d : l) {
                if(d == null) continue;
           else if(d instanceof Import)   {  imports =  imports.add((Import)d);   }
           else if(d instanceof Logs)     {
             Logs newLogs = (Logs)d;

             // check if there is a new host logger
             if(!(newLogs.host() instanceof NONE))
                 // set host logger, if none was specified, otherwise throw error
                 if(logs.host() instanceof NONE)
                     logs = logs.replaceHost(newLogs.host());
                 else error("duplicate definition of host logger", lleft, lright);

             // check if there is a new board logger
             if(!(newLogs.board() instanceof NONE))
                 // set board logger, if none was specified, otherwise throw error
                 if(logs.board() instanceof NONE)
                     logs = logs.replaceBoard(newLogs.board());
                 else error("duplicate definition of board logger", lleft, lright);
           }
           else if(d instanceof Option)   {     opts =     opts.add((Option)d);   }
           else if(d instanceof Core)     {    cores =    cores.add((Core)d);     }
           else if(d instanceof Instance) {    insts =    insts.add((Instance)d); }
           else if(d instanceof GPIO)     {    gpios =    gpios.add((GPIO)d);     }
           else if(d instanceof Medium)   {
             // TODO catch duplicates?
             // check, if the medium is already defined. If so, throw exception
             if(medium instanceof NONE) medium = (Medium)d;
             else error("Duplicate communication medium", lleft, lright);
           }
           else if(d instanceof Scheduler) {
             if(scheduler instanceof USER_DEFINED) error("Duplicate scheduler", lleft, lright);
             scheduler = (Scheduler)d;
           }
         }
         RESULT = BDL.BDLFile(checksum, imports, logs, opts, cores, gpios, insts, medium, scheduler);
     :}
  ;

decl_list ::=
     decl:d decl_list:l
     {:
         if(d == null) RESULT = l;
         else RESULT = l.appFront(d);
     :}
  |
     {:
         RESULT = BDL.Decls();
     :}
  ;

decl ::=
     imp:i       {: RESULT = i; :}
  |  log:l       {: RESULT = l; :}
  |  option:o    {: RESULT = o; :}
  |  core:c      {: RESULT = c; :}
  |  instance:i  {: RESULT = i; :}
  |  gpio:g      {: RESULT = g; :}
  |  medium:m    {: RESULT = m; :}
  |  scheduler:s {: RESULT = s; :}
  ;

imp ::=
     IMPORT STRING_LITERAL:imp
     {:
         RESULT = BDL.Import(imp, pos(impleft));
     :}
  ;

option_list ::=
     BEGIN option_list_part:l END
     {:
         RESULT = l;
     :}
  |
     {:
         RESULT = BDL.Options();
     :}
  ;

option_list_part ::=
     option:o option_list_part:l
     {:
         RESULT = l.appFront(o);
     :}
  |
     {:
         RESULT = BDL.Options();
     :}
  ;

option ::=
     SWQUEUE:key DEC:val
     {:
         RESULT = BDL.SWQUEUE(pos(keyleft),val);
     :}
  |  HWQUEUE:key DEC:val
     {:
         RESULT = BDL.HWQUEUE(pos(keyleft),val);
     :}
  |  WIDTH:key DEC:val
     {:
         RESULT = BDL.BITWIDTH(pos(keyleft),val);
     :}
  |  POLL:key DEC:val
     {:
         RESULT = BDL.POLL(pos(keyleft), val);
     :}

  |  POLL:key
     {:
         RESULT = BDL.POLL(pos(keyleft), 0);
     :}
  ;

log ::=
  LOG:key device:dev severity:sev
     {:
         Log log = BDL.CONSOLE(pos(keyleft), sev);
         if(dev) RESULT = BDL.Logs(log, BDL.NONE());
         else    RESULT = BDL.Logs(BDL.NONE(), log);
     :}
  |  LOG:key device:dev severity:sev STRING_LITERAL:file
     {:
         Log log = BDL.FILE(pos(keyleft), sev, file);
         if(dev) RESULT = BDL.Logs(log, BDL.NONE());
         else    RESULT = BDL.Logs(BDL.NONE(), log);
     :}
  ;

severity ::=
     {:
         RESULT = BDL.ERROR();
     :}
  |  ID:id
     {:
         if(id.equals("error"))        { RESULT = BDL.ERROR();  }
         else if(id.equals("warning")) { RESULT = BDL.WARN();   }
         else if(id.equals("info"))    { RESULT = BDL.INFO();   }
         else if(id.equals("fine"))    { RESULT = BDL.FINE();   }
         else if(id.equals("finer"))   { RESULT = BDL.FINER();  }
         else if(id.equals("finest"))  { RESULT = BDL.FINEST(); }
         else error("unknown severity " + id, idleft, idright);
     :}
  ;
device ::=
     ID:id
     {:
         if(id.equals("host"))       { RESULT = true;  }
         else if(id.equals("board")) { RESULT = false; }
         else error("unknown device specifier " + id, idleft, idright);
     :}
  ;

core ::=
     CORE ID:id version:v BEGIN source_list:s port_list:p END
     {:
         RESULT = BDL.Core(id, v, pos(idleft), s, p);
     :}
  |  CORE:c error END
     {:
         error("expected core identifier after keyword", cleft, cright); 
     :}
  |  CORE:c ID error END
     {:
         error("expected version string after core identifier", cleft, cright);
     :}
  |  CORE:c ID version BEGIN error END
     {:
         error("error inside core declaration block", cleft, cright);
     :}
  ;

version ::=
     DEC:a VER:b VER:c
     {:
         RESULT = String.valueOf(a) + b + c;
     :}
  ;

source_list ::=
     source:s source_list:l
     {:
         RESULT = s.addAll(l);
     :}
  |  source:s
     {:
         RESULT = s;
     :}
  ;

string_list ::=
     STRING_LITERAL:s COMMA string_list:sl
     {:
          RESULT = sl.appFront(s);
     :}
  |  STRING_LITERAL:s
     {:
          RESULT = BDL.Strings(s);
     :}
  ;

source ::=
     SOURCE:s string_list:path
     {:
         RESULT = BDL.Imports();
         for(String imp : path) RESULT = RESULT.add(BDL.Import(imp, pos(sleft)));
     :}
  |  SOURCE:s error
     {:
         error("source attribute is missing path", sleft, sright);
     :}
  ;

port_list ::=
     port:p port_list:l
     {:
         RESULT = p.addAll(l);
     :}
  |
     {:
         RESULT = BDL.Ports();
     :}
  ;

id_list ::=
     id:id id_list_part:l
     {:
          RESULT = l.appFront(id);
     :}
  ;
 
id_list_part ::=
     COMMA id:id id_list_part:l
     {:
         RESULT = l.appFront(id);
     :}
  |
     {:
         RESULT = BDL.Strings();
     :}
  ;

id ::=
      ID:id
     {:
         RESULT = id;
     :}
  |  direct:id
     {:
        RESULT = id.Switch(new Direction.Switch<String, NE>() {
           public String CaseIN  (IN   term) { return "in";   }
           public String CaseOUT (OUT  term) { return "out";  }
           public String CaseDUAL(DUAL term) { return "dual"; }
        });
     :}
  ;

port   ::=
     PORT:p direct:d id_list:il option_list:opts
     {: 
         RESULT = BDL.Ports();
         for(String id : il) RESULT = RESULT.add(BDL.AXI(id, pos(pleft), d, opts));
     :}
  |  PORT:p ID option_list
     {:
         error("port is missing a valid direction specifier", pleft, pright);
     :}
  |  PORT:p direct option_list
     {:
         error("port is missing an identifier", pleft, pright);
     :}
  |  CLK:c ID:i DEC:val
     {:
         RESULT = BDL.Ports(BDL.CLK(i, pos(cleft), val));
     :}
  |  CLK:c CLK:i DEC:val
     {:
         RESULT = BDL.Ports(BDL.CLK("clk", pos(cleft), val));
     :}
  |  RST:r ID:i DEC:val
     {:
         boolean polarity = false;
         switch(val) {
         case 0: polarity = false; break;
         case 1: polarity = true;  break;
         default: error("invalid reset polarity", rleft, rright);
         }
         RESULT = BDL.Ports(BDL.RST(i, pos(rleft), polarity));
     :}
  |  RST:r RST:i DEC:val
     {:
         boolean polarity = false;
         switch(val) {
         case 0: polarity = false; break;
         case 1: polarity = true;  break;
         default: error("invalid reset polarity", rleft, rright);
         }
         RESULT = BDL.Ports(BDL.RST("rst", pos(rleft), polarity));
     :}
  ;

gpio ::=
     GPIO ID:id code_block:cb
     {:
         RESULT = BDL.GPIO(id, pos(idleft), cb);
     :}
  |  GPIO:g direct code_block
     {:
         error("Invalid ID for GPIO component", gleft, gright);
     :}
  ;

direct ::=
     IN   {: RESULT = BDL.IN();   :}
  |  OUT  {: RESULT = BDL.OUT();  :}
  |  DUAL {: RESULT = BDL.DUAL(); :}
  ;

instance ::=
     INSTANCE ID:core version:ver ID:id bind_block:b
     {:
         RESULT = BDL.Instance(id, core, ver, pos(idleft), b);
     :}
  ;

bind_block ::=
     BEGIN bind_list_part:l END
     {:
         RESULT = l;
     :}
  |
     {:
         RESULT = BDL.Bindings();
     :}
  ;

bind_list_part ::=
     bind:b bind_list_part:l
     {:
         RESULT = b.addAll(l);
     :}
  |
     {:
         RESULT = BDL.Bindings();
     :}
  ;

bind ::=
     BIND id:port ID:axis option_list:opts
     {:
         RESULT = BDL.Bindings(BDL.Axis(axis, port, pos(portleft), opts));
     :}
  |
     BIND:b id:port option_list:opts
     {:
         error("axis declaration missing port/axis identifier. Use \"cpu\" for processor connected port", bleft, bright);
         RESULT = BDL.Bindings(BDL.CPUAxis(port, pos(portleft), opts));
     :}
  |  CPU:c id_list:ports option_list:opts
     {:
         RESULT = BDL.Bindings();
         for(String port : ports) RESULT = RESULT.add(BDL.CPUAxis(port, pos(cleft), opts));
     :}
  |  CPU:c ID:id1 ID:id2 option_list:opts
     {:
         error("cpu axis declaration with two identifiers. Use \"bind\" for non-processor connected ports or separate multiple processor-connected ports with comma", cleft, cright);
         RESULT = BDL.Bindings(
           BDL.CPUAxis(id1, pos(id1left)),
           BDL.CPUAxis(id2, pos(id2left))
         );
     :}
  ;

code_block ::=
     CBEGIN CEND:c
     {:
         RESULT = BDL.USER_DEFINED(c);
     :}
  |  CBEGIN:b error
     {:
         error("could not find matching closing code bracket", bleft, bright);
         RESULT = BDL.DEFAULT();
     :}
  |  error:e
     {:
         error("expected opening code block", eleft, eright);
         RESULT = BDL.DEFAULT();
     :}
  |
     {:
         RESULT = BDL.DEFAULT();
     :}
  ;

medium ::= // TODO
     MEDIUM ID:id medium_option_list:mol
     {:
         if(id.equals("ethernet"))  RESULT = BDL.ETHERNET(pos(idleft),mol);
         else if(id.equals("uart")) RESULT = BDL.UART(pos(idleft),mol);
         else if(id.equals("pcie")) RESULT = BDL.PCIE(pos(idleft),mol);
         else {
            error("unknown medium " + id, idleft, idright);
            RESULT = BDL.NONE();
         }
     :}
  ;

medium_option_list ::=
     BEGIN medium_option_list_part:mol END
     {:
         RESULT = mol;
     :}
  |
     {:
         RESULT = BDL.MOptions();
     :}
  ;

medium_option_list_part ::=
     medium_option:mo medium_option_list_part:mol
     {:
         RESULT = mol.appFront(mo);
     :}
  |
     {:
         RESULT = BDL.MOptions();
     :}
  ;

medium_option ::=
     MAC:key STRING_LITERAL:val
     {:
         RESULT = BDL.MAC(pos(keyleft),val);
     :}
  |  IP:key STRING_LITERAL:val
     {:
         RESULT = BDL.IP(pos(keyleft),val);
     :}
  |  MASK:key STRING_LITERAL:val
     {:
         RESULT = BDL.MASK(pos(keyleft),val);
     :}
  |  GATE:key STRING_LITERAL:val
     {:
         RESULT = BDL.GATE(pos(keyleft),val);
     :}
  |  PORT:key DEC:val
     {:
         RESULT = BDL.PORTID(pos(keyleft),val);
     :}
  |  TIMEOUT:key DEC:val
     {:
         RESULT = BDL.TOUT(pos(keyleft),val);
     :}
  |  DHCP:key
     {:
         RESULT = BDL.DHCP(pos(keyleft),-1);
     :}
  |  DHCP:key DEC:tout
     {:
         RESULT = BDL.DHCP(pos(keyleft),tout);
     :}
  ;

scheduler ::=
     SCHEDULER:s code_block:cb
     {:
         RESULT = BDL.Scheduler(pos(sleft), cb);
     :}
  ;

