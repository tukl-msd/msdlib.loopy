specification BDL

// comments within backend bocks (esp. in the java backend block)
// unterminated strings ("abc) - why does katja even need strings??
// weird behaviour concerning cli parameter parses (cause unknowns are passed on and on and non-flags are always interpreted as additional files at the end;)

backend java {
  package de.hopp.generator.model
  import java.lang.String
  import java.lang.Integer
  import java.lang.Boolean
}

root BDLFile Pos

external String
external Integer
external Boolean

BDLFile (Checksum checksum, Imports imports, Logs logs, Options opts, Cores cores, GPIOs gpios, Instances insts, Medium medium, Scheduler scheduler)

Checksum (String checksum)

Decl = Import | Logs | Option | Core | GPIO | Instance | Medium | Scheduler

Import (String file, Position pos)

// Definition of a new ipcore
Core (String name, String version, Position pos, Imports source, Ports ports)

Port = AXI(String name, Position pos, Direction direction, Options opts)
     | CLK(String name, Position pos, Integer frequency)
     | RST(String name, Position pos, Boolean polarity)

Direction = IN() | OUT() | DUAL()

// Instance of an ipcore
Instance(String name, String core, String version, Position pos, Bindings bind)

Binding = Axis(String port, String axis, Position pos, Options opts)
        | CPUAxis(String port, Position pos, Options opts)

GPIO (String name, Position pos, Code callback)

NONE()

// Definition of the attached medium
Medium = NONE
       | DefinedMedium
DefinedMedium = ETHERNET(Position pos, MOptions opts)
              | UART    (Position pos, MOptions opts)
              | PCIE    (Position pos, MOptions opts)

// General constants and flags for board and ports (care for invalid combinations)
Option = HWQUEUE (Position pos, Integer qsize)
       | SWQUEUE (Position pos, Integer qsize)
       | BITWIDTH(Position pos, Integer bit)
       | POLL    (Position pos, Integer count)


Logs (Log host, Log board)
Log = NONE
    | DefinedLog
DefinedLog = CONSOLE (Position pos, LogSeverity sev)
           | FILE    (Position pos, LogSeverity sev, String file)

LogSeverity = ERROR()
            | WARN()
            | INFO()
            | FINE()
            | FINER()
            | FINEST()

MOption = MAC    (Position pos, String val)
        | IP     (Position pos, String val)
        | MASK   (Position pos, String val)
        | GATE   (Position pos, String val)
        | PORTID (Position pos, Integer val)
        | TOUT   (Position pos, Integer val)
        | DHCP   (Position pos, Integer tout)

Scheduler (Position pos, Code code)

Code = DEFAULT() | USER_DEFINED(Strings content)

Position (String filename, Integer line)

// List types
Imports   * Import
Options   * Option
MOptions  * MOption
Cores     * Core
GPIOs     * GPIO
Instances * Instance
Ports     * Port
Strings   * String
Bindings  * Binding
Decls     * Decl
